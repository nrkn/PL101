start =
    expression

repeat = 
    "*" ignore* count: integer ignore* expr: expression
        { 
          return {
            tag: 'repeat',
            count: count,
            section: expr
          }; 
        }

startlist =
    ignore* "(" ignore*
    
endlist =   
    ignore* ")" ignore*        
        
par =
    startlist start: expression rest: parsubs* endlist
        { 
          var result = [ start ].concat( rest ); 
          
          if( result.length === 1 ) {
            return result[ 0 ];
          }
          
          var par = {
                tag: 'par',
                left: {},
                right: {}
              },
              lastPar = par,
              thisPar = {};
          
          for( var i = 0; i < result.length; i++ ) {
            if( i < result.length - 1 ) {
              thisPar = lastPar;
              lastPar.left = result[ i ];
              lastPar = lastPar.right = {
                tag: 'par',
                left: {},
                right: {}
              };
            } else {
              thisPar.right = result[ i ];
            }
          }     
          
          return par;        
        }
        
parsubs = 
    ignore* "," ignore* expr: expression 
        { return expr; }
        
expression =  
    par
  / repeat
  / notes
  
notes =
    subsequences: subsequence*
        {
          var result = [],
              initial = {
                modifier: { arg: 'n' },
                octave: { arg: 4 },
                len: { arg: 1 },
                tempo: { arg: 120 }                
              };
          
          for( var i = 0; i < subsequences.length; i++ ) {
            var subsequence = subsequences[ i ],
                defines = subsequence.defines,
                elements = subsequence.elements;
                
            //if the first element in the sequence has any undefineds, use the initial settings
            if( i == 0 ) {
              defines.modifier = defines.modifier ? defines.modifier : initial.modifier;
              defines.octave = defines.octave ? defines.octave : initial.octave;
              defines.len = defines.len ? defines.len : initial.len;
              defines.tempo = defines.tempo ? defines.tempo : initial.tempo;              
            } else {
              //otherwise if an element has any undefined, copy them from the previous element
              defines.modifier = defines.modifier ? defines.modifier : subsequences[ i  - 1 ].defines.modifier;
              defines.octave = defines.octave ? defines.octave : subsequences[ i  - 1 ].defines.octave;
              defines.len = defines.len ? defines.len : subsequences[ i  - 1 ].defines.len;
              defines.tempo = defines.tempo ? defines.tempo : subsequences[ i  - 1 ].defines.tempo;
            }
            
            var msPerBeat = 60000 / ( defines.tempo.arg / 4 );
            
            for( var e = 0; e < elements.length; e++ ) {
              var element = elements[ e ],
                  musElement = {
                    tag: element.tag,
                    dur: msPerBeat / ( element.duration !== '' ? element.duration : defines.len.arg )
                  };
                  
              //each . in the mml format extends the duration by half again
              if( element.extend > 0  ) {
                musElement.dur *= Math.pow( 1.5, element.extend );
              }

              if( element.tag === 'note' ) {
                if( defines.shiftoctave ) {
                  defines.octave.arg += ( defines.shiftoctave.arg === '<' ? -1 : 1 );
                  defines.shiftoctave = null;
                }
                
                defines.octave.arg = ( defines.octave.arg < 0 ) ? 0 : ( defines.octave.arg > 6 ) ? 6 : defines.octave.arg;
                
                var octave = element.octave !== undefined ? element.octave : defines.octave.arg;
                
                musElement.pitch = element.note + element.accidental + octave; 
                //legato
                if( [ 'l', 'f', 'b' ].indexOf( defines.modifier.arg ) !== -1 ) {
                  result.push( musElement );
                } else {          
                  //staccato or normal
                  var mod = defines.modifier.arg === 's' ? 0.75 : 0.875,
                      restMod = 1 - mod;
                      rest = {
                        tag: 'rest',
                        dur: musElement.dur * restMod
                      };
                  musElement.dur *= mod;
                  result.push( musElement );
                  result.push( rest );                
                }
              } else {
                result.push( musElement );
              }               
            }
          }
          
          if( result.length === 0 ) {
            return undefined;
          }
          
          if( result.length === 1 ) {
            return result[ 0 ];
          }

          var mus = {
                tag: 'seq',
                left: {},
                right: {}
              },
              lastSeq = mus,
              thisSeq = {};
          
          for( var i = 0; i < result.length; i++ ) {
            if( i < result.length - 1 ) {
              thisSeq = lastSeq;
              lastSeq.left = result[ i ];
              lastSeq = lastSeq.right = {
                tag: 'seq',
                left: {},
                right: {}
              };
            } else {
              thisSeq.right = result[ i ];
            }
          }     
          
          return mus;
        }        

digit =
    [0-9]
    
integer =
    digits: digit+ 
        { return parseInt( digits.join( '' ), 10 ); }
        
whitespace =
    [ \t\r\n]

comment =
    ';;' [^\r\n]*

ignore =
    whitespace
  / comment   
    
noteletter =
    letter: [a-g]i
        { return letter.toLowerCase(); }

accidental =
    sign: [+#-]
        { return sign === '+' || sign === '#' ? '#' : 'b'; }
        
duration =
    integer
        
note =
    numberedNote
  / ignore* noteletter: noteletter accidental: accidental? duration: duration? extend: '.'* ignore*
        { 
          return { 
            tag: 'note',
            note: noteletter, 
            accidental: accidental,
            duration: duration,
            extend: extend.length
          };
        }

numberedNote = 
    ignore* 'n'i num: integer ignore*       
        {
          num = ( num < 0 ) ? 0 : ( num > 84 ) ? 84 : num;
          
          if( num === 0 ) {
            return {
              tag: 'rest',
              duration: '',
              extend: 0
            };
          }          
          
          var notes =       'ccdeeffgaabb',
              accidentals = ' # b  # b b ';
              noteletter = ( num % 12 == 0 ) ? 'b' : notes[ ( num % 12 ) - 1 ],
              accidental = ( num % 12 == 0 ) ? ' ' : accidentals[ ( num % 12 ) - 1 ],
              octave = Math.floor( ( num - 1 ) / 12 );
              
          var result = {
            tag: 'note',
            note: noteletter, 
            accidental: accidental === ' ' ? '' : accidental,
            duration: '',
            octave: octave
          };          

          return result;          
        }
        
rest =
    ignore* [pr]i duration: duration? extend: '.'* ignore*
        {
          return {
            tag: 'rest',
            duration: duration,
            extend: extend.length
          };
        }    

element =
    note
  / rest 

modifier =
    ignore* 'm'i ignore* arg: [fblsn]i ignore* 
        {  
          return {
            tag: 'modifier',
            arg: arg
          };
        }
        
tempo =
    ignore* 't'i ignore* arg: integer ignore* 
        {
          return {
            tag: 'tempo',
            arg: arg
          };
        }
        
octave =
    ignore* 'o'i ignore* arg: [0-6] ignore* 
        {
          return {
            tag: 'octave',
            arg: parseInt( arg, 10 )
          }
        }
        
len =
    ignore* 'l'i ignore* arg: duration ignore* 
        {
          return {
            tag: 'len',
            arg: arg
          };
        }

shiftoctave =
    ignore* arg:[<>] ignore*
      {
        return {
          tag: 'shiftoctave',
          arg: arg
        };
      }
        
define =
    modifier
  / tempo
  / octave
  / len
  / shiftoctave

defines = 
    defines: define* 
        {
          var result = {};
        
          
          for( var i = 0; i < defines.length; i++ ) {
            result[ defines[ i ].tag ] = defines[ i ];
          }
          
          
          return result;
        }

subsequence =
    defines: defines elements: element+
        { 
          return {
            tag: 'subsequence',
            defines: defines,
            elements: elements
          };
        }